/*
   Copyright 2017 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

import java.nio.file.Paths
import java.text.SimpleDateFormat

apply from: "$rootDir/gradle/utilities.gradle"

String getCurrentGitCommit() {
    return runProcess('git', 'describe', '--always', '--dirty', '--exclude', '*', '--abbrev=0')
}

String getCurrentGitCommitDate() {
    return runProcess('git', 'show', '-s', '--format=%ci', 'HEAD')
}

String getBuildDate() {
    if (project.hasProperty("releaseBuild")) {
        def dateFormat = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss Z")
        dateFormat.setTimeZone(new SimpleTimeZone(SimpleTimeZone.UTC_TIME, "UTC"))
        return dateFormat.format(new Date())
    } else {
        return "(the build date is not populated for development builds)"
    }
}

String versionInfoFileContents() {
    kotlinLicenseHeader +
    """ |package batect
        |
        |import batect.utils.Version
        |
        |// This file is generated automatically by the $generateVersionInfoFile.name Gradle task.
        |// Do not edit it (it will be overwritten on the next build anyway).
        |// Do not commit it.
        |class VersionInfo {
        |    val gitCommitHash = "${getCurrentGitCommit()}"
        |    val gitCommitDate = "${getCurrentGitCommitDate()}"
        |    val version = Version.parse("$project.version")
        |    val buildDate = "${getBuildDate()}"
        |}
        |""".stripMargin("|")
}

def versionInfoFile = Paths.get("$projectDir/src/main/kotlin/batect/VersionInfo.kt").toFile()

task generateVersionInfoFile {
    inputs.property("version", project.version)
    outputs.file versionInfoFile

    outputs.upToDateWhen {
        versionInfoFile.exists() && versionInfoFile.text == versionInfoFileContents()
    }

    doLast {
        def text = versionInfoFileContents()
        versionInfoFile.text = text
    }
}

compileKotlin.dependsOn generateVersionInfoFile

task cleanVersionInfo(type: Delete) {
    delete versionInfoFile
}

clean.dependsOn cleanVersionInfo
