/*
   Copyright 2017-2018 Charles Korn.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

buildscript {
    ext.spek_version = '1.1.5'
    ext.serialization_version = '0.5.1'

    repositories {
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }

    dependencies {
        classpath "org.jetbrains.kotlinx:kotlinx-gradle-serialization-plugin:$serialization_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.50'
    id 'com.github.ben-manes.versions' version '0.18.0'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

apply plugin: 'application'
apply plugin: 'kotlinx-serialization'

applicationName = 'batect'
archivesBaseName = 'batect'
mainClassName = 'batect.ApplicationKt'

repositories {
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

dependencies {
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    // Override the version of kotlin-reflect used by jackson-module-kotlin.
    // We don't have to specify a version ourselves (the Kotlin plugin will do that for us),
    // but if we don't reference it here, the version that jackson-module-kotlin refers to
    // is used.
    compile 'org.jetbrains.kotlin:kotlin-reflect'
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.6'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.6'
    compile 'org.kodein.di:kodein-di-generic-jvm:5.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.10.0'

    testCompile 'com.natpryce:hamkrest:1.4.2.2'
    testCompile 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testCompile 'com.google.jimfs:jimfs:1.1'
    testCompile "org.jetbrains.spek:spek-api:$spek_version"

    testCompile("org.jetbrains.spek:spek-data-driven-extension:$spek_version") {
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntime "org.jetbrains.spek:spek-junit-platform-engine:$spek_version"
    testRuntime 'org.junit.platform:junit-platform-engine:1.2.0'
}

tasks.withType(Test) {
    useJUnitPlatform {
        includeEngines 'spek'
    }

    testLogging {
        events "failed"
        events "skipped"
        events "standard_out"
        events "standard_error"

        showExceptions true
        showStackTraces true
        showCauses true
        exceptionFormat "full"
    }
}

shadowJar {
    classifier = null
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply from: 'gradle/journeyTest.gradle'
apply from: 'gradle/spotless.gradle'
apply from: 'gradle/versionInfo.gradle'
