import org.junit.platform.console.ConsoleLauncher

buildscript {
    ext.kotlin_version = '1.1.3'
    ext.junit_version = '1.0.0-M4'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_version"
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.junit.platform.gradle.plugin'

mainClassName = 'decompose.ApplicationKt'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" // Override the version of kotlin-reflect used by jackson-module-kotlin

    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.8'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.8'
    compile 'com.github.salomonbrys.kodein:kodein:4.0.0'

    testCompile 'com.natpryce:hamkrest:1.4.0.0'
    testCompile 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'
    testCompile 'org.jetbrains.spek:spek-api:1.1.1'
    testCompile 'com.google.jimfs:jimfs:1.1'

    testCompile('org.jetbrains.spek:spek-data-driven-extension:1.1.1') {
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntime('org.jetbrains.spek:spek-junit-platform-engine:1.1.1') {
        exclude group: 'org.junit.platform'
    }

    // Temporary workaround for issue running tests in IntelliJ: https://github.com/raniejade/spek-idea-plugin/issues/37
    testRuntime "org.junit.platform:junit-platform-launcher:$junit_version"
}

junitPlatform {
    platformVersion "$junit_version"

    filters {
        engines {
            include 'spek'
        }
    }
}

sourceSets {
    journeyTest
}

configurations {
    journeyTestCompile.extendsFrom testCompile
    journeyTestRuntime.extendsFrom testRuntime
}

// Gross hack because the JUnit 5 Gradle plugin doesn't provide a task class that we can extend from,
// and the built-in Gradle Test task doesn't support JUnit 5.
// Based on https://github.com/junit-team/junit5/blob/master/junit-platform-gradle-plugin/src/main/groovy/org/junit/platform/gradle/plugin/JUnitPlatformPlugin.groovy
task journeyTest(type: JavaExec) {
    description 'Runs the journey tests.'
    group 'Verification'

    dependsOn journeyTestClasses
    dependsOn installDist

    classpath = sourceSets.journeyTest.runtimeClasspath + configurations.junitPlatform

    def reportDir = "$buildDir/test-results/journey-tests"
    outputs.dir reportDir

    def scanDirs = [
            sourceSets.journeyTest.output.classesDir,
            sourceSets.journeyTest.output.resourcesDir
    ]
    scanDirs += sourceSets.journeyTest.output.dirs.files

    main = ConsoleLauncher.class.getName()
    args = [
            '--scan-class-path', scanDirs.join(File.pathSeparator),
            '--reports-dir', reportDir,
            '--include-engine', 'spek',
            '--details', 'none'
    ]
}
